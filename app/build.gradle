import com.track.buildsrc.Libraries
import com.track.buildsrc.Modules
import com.track.buildsrc.AndroidLibraries
import com.track.buildsrc.TestLibraries
import com.track.buildsrc.SdkVersions

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id("kotlin-kapt")
    id("dagger.hilt.android.plugin")
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

// Note: 2 digits max for each value
ext.versionMajor = 0
ext.versionMinor = 2
ext.versionPatch = 1


android {
    signingConfigs {
        debug {
            storeFile file('./signature/trackhabit.debug.keystore.jks')
            storePassword 'trackhabitsquad1'
            keyAlias 'trackhabit'
            keyPassword 'trackhabitsquad1'
        }
        release {
            storeFile file('./signature/trackhabit.release.keystore.jks')
            storePassword 'trackhabitsquad1'
            keyAlias 'trackhabit'
            keyPassword 'trackhabitsquad1'
        }
    }
    compileSdk SdkVersions.compileSdkVersion

    defaultConfig {
        applicationId "com.track.trackhabit"
        minSdk SdkVersions.minSdkVersion
        targetSdk SdkVersions.targetSdkVersion

        versionCode 3
        versionName "$versionMajor.$versionMinor.$versionPatch"

        testInstrumentationRunnerArguments clearPackageData: 'true'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug

        multiDexEnabled true
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Track Habit dbg"
            signingConfig signingConfigs.debug
        }

        release {
            resValue "string", "app_name", "Track Habit"

            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                // We do not activate obfuscation as it makes it hard then to read crash reports, and it's a bit useless on an open source project :)
                obfuscate false
                optimizeCode true
                proguardFiles 'proguard-rules.pro'
            }
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility SdkVersions.javaVersion
        targetCompatibility SdkVersions.javaVersion
    }
    kotlinOptions {
        jvmTarget = SdkVersions.jvmTarget
    }

    buildFeatures {
        dataBinding true
    }

}

dependencies {

    implementation AndroidLibraries.coreKtx
    implementation AndroidLibraries.appCompat
    implementation AndroidLibraries.material
    implementation AndroidLibraries.constraintLayout
    implementation AndroidLibraries.hawk
    testImplementation TestLibraries.junit
    debugImplementation Libraries.hyperionCore
    debugImplementation Libraries.hyperionCrash
    debugImplementation Libraries.hyperionMeasurement
    androidTestImplementation TestLibraries.ext
    androidTestImplementation TestLibraries.espresso
    implementation project(Modules.trackHabit)
    implementation project(Modules.navigation)
    implementation project(Modules.common)
    implementation project(Modules.remote)
    implementation project(Modules.authentication)

    implementation Libraries.Timber

    implementation Libraries.daggerHilt
    implementation AndroidLibraries.kotlinCoroutineAndroid
    kapt Libraries.hiltCompiler

    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:29.0.2')

    // Declare the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
}
kapt {
    correctErrorTypes true
}